PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?median ?area
WHERE {
  ?x <http://purl.org/linked-data/cube#dataSet> <http://statistics.gov.scot/data/earnings> ;
     <http://statistics.gov.scot/def/dimension/gender> ?gender ;
     <http://purl.org/linked-data/sdmx/2009/dimension#refPeriod> ?refPeriod ;
     <http://purl.org/linked-data/sdmx/2009/dimension#refArea> ?refArea ;
     <http://statistics.gov.scot/def/dimension/populationGroup> ?popGroup ;
     <http://statistics.gov.scot/def/measure-properties/median> ?median .
  ?refArea skos:notation ?area.

  FILTER (?gender = <http://statistics.gov.scot/def/concept/gender/all>)
  FILTER (?refPeriod = <http://reference.data.gov.uk/id/year/2012>)
  FILTER (?popGroup = <http://statistics.gov.scot/def/concept/population-group/residence-based>)
}


PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?median ?area
WHERE {
  ?x <http://purl.org/linked-data/cube#dataSet> <http://statistics.gov.scot/data/house-sales-prices> ;
     <http://purl.org/linked-data/sdmx/2009/dimension#refPeriod> ?refPeriod ;
     <http://purl.org/linked-data/sdmx/2009/dimension#refArea> ?refArea ;
     <http://statistics.gov.scot/def/measure-properties/mean> ?median .
  ?refArea skos:notation ?area ;
           <http://statistics.data.gov.uk/def/statistical-entity#code> ?code.

  FILTER (?refPeriod = <http://reference.data.gov.uk/id/year/2013>)
  FILTER (?code = <http://statistics.gov.scot/id/statistical-entity/S16>)
}


PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?vigintile ?area ?const
WHERE {
  ?x <http://purl.org/linked-data/cube#dataSet> <http://statistics.gov.scot/data/scottish-index-multiple-deprivation> ;
     <http://statistics.gov.scot/def/dimension/simdDomain> ?simdDomain ;
     <http://purl.org/linked-data/sdmx/2009/dimension#refPeriod> ?refPeriod ;
     <http://statistics.gov.scot/def/measure-properties/vigintile> ?vigintile ;
     <http://purl.org/linked-data/sdmx/2009/dimension#refArea> ?refArea .
  ?refArea <http://statistics.gov.scot/def/hierarchy/best-fit#scottish-parliamentary-consituency> ?pc ;
           skos:notation ?area .
  ?pc skos:notation ?const .

  FILTER (?refPeriod = <http://reference.data.gov.uk/id/year/2012>)
  FILTER (?simdDomain = <http://statistics.gov.scot/def/concept/simd-domain/overall>)
}
